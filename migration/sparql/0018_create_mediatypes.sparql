# 1) Create media type for comic books

SPARQL
PREFIX : <http://__HOST__/ontology#>
PREFIX format: <http://data.deichman.no/format#>
PREFIX mediaType: <http://data.deichman.no/mediaType#>
PREFIX literaryForm: <http://data.deichman.no/literaryForm#>

WITH <http://deichman.no/migration>

MODIFY

DELETE {
  ?pub :literaryForm literaryForm:comicBook .
  ?pub :format format:Book .
}

INSERT {
  ?pub :hasMediaType mediaType:ComicBook .
}

WHERE {
  ?pub :literaryForm literaryForm:comicBook ;
    a :Publication .
  OPTIONAL {?pub :format format:Book .}
}
;


# 2) Construct media type based on format, keep format

SPARQL
PREFIX : <http://__HOST__/ontology#>
PREFIX format: <http://data.deichman.no/format#>
PREFIX mediaType: <http://data.deichman.no/mediaType#>

WITH <http://deichman.no/migration>

INSERT {
  ?pub :hasMediaType ?mediaType
}

WHERE {
    ?pub a :Publication ;
         :format ?format  .

    VALUES (?format ?mediaType) {
      (format:DAISY                   mediaType:Audiobook)
      (format:EBokBib                 mediaType:E-book)
      (format:ComputerFile            mediaType:E-book)
      (format:Videotape               mediaType:Film)
      (format:DVD                     mediaType:Film)
      (format:Blu-ray                 mediaType:Film)
      (format:3DMovie                 mediaType:Film)
      (format:PersonalComputerGame    mediaType:Game)
      (format:Playstation2Game        mediaType:Game)
      (format:Playstation3Game        mediaType:Game)
      (format:Playstation4Game        mediaType:Game)
      (format:Xbox360Game             mediaType:Game)
      (format:XboxOneGame             mediaType:Game)
      (format:NintendoDSGame          mediaType:Game)
      (format:NintendoWiiGame         mediaType:Game)
      (format:Diapositive             mediaType:Other)
      (format:Microfiche              mediaType:Other)
      (format:MicrofilmReel           mediaType:Other)
      (format:Poster                  mediaType:Other)
      (format:Map                     mediaType:Other)
    }
}
;


# 3) Replace format with Media Type

SPARQL
PREFIX : <http://__HOST__/ontology#>
PREFIX format: <http://data.deichman.no/format#>
PREFIX mediaType: <http://data.deichman.no/mediaType#>

WITH <http://deichman.no/migration>

MODIFY 

DELETE {
  ?pub :format ?format .
}

INSERT {
  ?pub :hasMediaType ?mediaType
}

WHERE {
    ?pub a :Publication ;
         :format ?format  .

    VALUES (?format ?mediaType) {
      (format:Book                    mediaType:Book)
      (format:Audiobook               mediaType:Audiobook)
      (format:E-book                  mediaType:E-book)
      (format:LanguageCourse          mediaType:LanguageCourse)
      (format:MusicRecording          mediaType:MusicRecording)
      (format:SheetMusic              mediaType:SheetMusic)
    }
}
;


# 4) Add media type and format for card games and board games

SPARQL
PREFIX : <http://__HOST__/ontology#>
PREFIX mediaType: <http://data.deichman.no/mediaType#>
PREFIX format: <http://data.deichman.no/format#>

WITH <http://deichman.no/migration>

MODIFY

DELETE {
  ?pub :genre <http://__HOST__/genre/g2049303700> .
  ?pub :genre <http://__HOST__/genre/g2049303800> .
  ?pub :format format:PhysicalBody .
}

INSERT {
  ?pub :hasMediaType mediaType:Game ;
    :format ?format .
}

WHERE {
  ?pub :format format:PhysicalBody ;
    :genre ?genre ;
    a :Publication .

  VALUES (?genre  ?format) {
    (<http://__HOST__/genre/g2049303700>   format:BoardGame)
    (<http://__HOST__/genre/g2049303800>   format:CardGame)
  }
}
;


# 5) Construct work type based on media type

SPARQL
PREFIX : <http://__HOST__/ontology#>
PREFIX mediaType: <http://data.deichman.no/mediaType#>
PREFIX workType: <http://data.deichman.no/workType#>

WITH <http://deichman.no/migration>

INSERT {
  ?pub :hasWorkType ?workType
}

WHERE {
    ?pub a :Publication ;
         :hasMediaType ?mediaType  .

    VALUES (?mediaType ?workType) {
      (mediaType:Film                 workType:Film)
      (mediaType:Game                 workType:Game)
      (mediaType:Audiobook            workType:Literature)
      (mediaType:Book                 workType:Literature)
      (mediaType:E-book               workType:Literature)
      (mediaType:ComicBook            workType:Literature)
      (mediaType:MusicRecording       workType:Music)
      (mediaType:SheetMusic           workType:Music)
    }
}
;
